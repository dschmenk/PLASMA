include "inc/cmdsys.plh"
include "inc/int32.plh"
include "inc/sincos.plh"
include "inc/lines.plh"

const CLIP_LEFT   = 1
const CLIP_RIGHT  = 2
const CLIP_TOP    = 4
const CLIP_BOTTOM = 8

var turtleX, turtleY, turtleAngle
byte pen
var clipLeft, clipRight, clipTop, clipBottom

def clipCode(x, y)#1
    byte code

    code = 0
    if x < clipLeft
        code = CLIP_LEFT
    elsif x > clipRight
        code = CLIP_RIGHT
    fin
    if y < clipTop
        code = code | CLIP_TOP
    elsif y > clipBottom
        code = code | CLIP_BOTTOM
    fin
    return code
end

def abs(val)#1
    return val < 0 ?? -val :: val
end

def lerp(a1, b1, a2, b2, n)#1
    word[2] i32

    if b2 == b1; return b1; fin // Horizontal lerp
    if a2 == a1; return n;  fin // Vertical lerp 
    loadi16(b2 - b1)
    muli16(n - a1)
    divi16(a2 - a1)
    store32(@i32)
    return i32[0] + b1
end

def clipLine(x1, y1, x2, y2)#0
    byte outcode1, outcode2

    outcode1 = clipCode(x1, y1)
    outcode2 = clipCode(x2, y2)
    if outcode1 & outcode2; return; fin // Early exit - clipped away
    if outcode1 | outcode2 // Something is getting clipped
        if outcode1 & CLIP_LEFT
            y1 = lerp(x1, y1, x2, y2, clipLeft)
            x1 = clipLeft
        elsif outcode1 & CLIP_RIGHT
            y1 = lerp(x1, y1, x2, y2, clipRight)
            x1 = clipRight
        fin
        if outcode1 & CLIP_TOP
            x1 = lerp(y1, x1, y2, x2, clipTop)
            y1 = clipTop
        elsif outcode1 & CLIP_BOTTOM
            x1 = lerp(y1, x1, y2, x2, clipBottom)
            y1 = clipBottom
        fin
        if outcode2 & CLIP_LEFT
            y2 = lerp(x1, y1, x2, y2, clipLeft)
            x2 = clipLeft
        elsif outcode2 & CLIP_RIGHT
            y2 = lerp(x1, y1, x2, y2, clipRight)
            x2 = clipRight
        fin
        if outcode2 & CLIP_TOP
            x2 = lerp(y1, x1, y2, x2, clipTop)
            y2 = clipTop
        elsif outcode2 & CLIP_BOTTOM
            x2 = lerp(y1, x1, y2, x2, clipBottom)
            y2 = clipBottom
        fin
        outcode1 = clipCode(x1, y1)
        outcode2 = clipCode(x2, y2)
        if outcode1 & outcode2; return; fin // Clipped away
    fin
    linespans(x1, y1, x2, y2)
end

export def clipRect(left, top, right, bottom)#0
    clipLeft = left
    clipTop  = top
    clipLeft = left
    clipBottom = bottom
end

export def initTurtle(width, height)#0
    cliprect(0, 0, width - 1, height - 1)
    turtleX      = width / 2
    turtleY      = height / 2
    turtleAngle  = 0
    pen          = 1
end

export def turnLeft(angle)#0
    turtleAngle = turtleAngle - angle
    while turtleAngle < 0; turtleAngle = turtleAngle + 360; loop
end

export def turnRight(angle)#0
    turtleAngle = turtleAngle + angle
    while turtleAngle >= 360; turtleAngle = turtleAngle - 360; loop
end

export def turnTo(angle)#0
    turtleAngle = angle
    while turtleAngle >= 360; turtleAngle = turtleAngle - 360; loop
    while turtleAngle < 0; turtleAngle = turtleAngle + 360; loop
end

export def moveForward(dist)#0
    var x, y

    x = turtleX + mulcos(dist, turtleAngle)
    y = turtleY + mulsin(dist, turtleAngle)
    if pen
        clipLine(turtleX, turtleY, x, y)
    fin
    turtleX = x
    turtleY = y
end

export def moveTo(x, y)#0
    if pen
        clipLine(turtleX, turtleY, x, y)
    fin
    turtleX = x
    turtleY = y
end

export def penUp#0
    pen = 0
end

export def penDown#0
    pen = 1
end

//
// Keep module in memory
//
return modkeep
done
