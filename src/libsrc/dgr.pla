import cmdsys
	predef puts, memset, memcpy, heapmark, heapalloc, heaprelease, call
	byte MACHID
	//
	// System flags: memory allocator screen holes.
	//
	const restxt1     = $0001
	const restxt2     = $0002
	const resxtxt1    = $0004
	const resxtxt2    = $0008
	const reshgr1     = $0010
	const reshgr2     = $0020
	const resxhgr1    = $0040
	const resxhgr2    = $0080
end
sysflags $000F	// Reserve all text pages
//
// Apple II hardware constants.
//
const showgraphics = $C050
const showtext     = $C051
const showfull     = $C052
const showmix      = $C053
const showpage1    = $C054
const showpage2    = $C055
const showlores    = $C056
const showhires    = $C057
const show40       = $C00C
const show80       = $C00D
const mapmain      = $C000
const mapaux       = $C001
const an3on        = $C05E
const an3off       = $C05F
const ena80        = $C07E
const dis80        = $C07F
const page1        = 0
const page2        = 1
//
// Screen row address arrays.
//
word[] dgr1rows     = $0400,$0480,$0500,$0580,$0600,$0680,$0700,$0780
word                = $0428,$04A8,$0528,$05A8,$0628,$06A8,$0728,$07A8
word                = $0450,$04D0,$0550,$05D0,$0650,$06D0,$0750,$07D0
word[] dgr2rows     = $0800,$0880,$0900,$0980,$0A00,$0A80,$0B00,$0B80
word                = $0828,$08A8,$0928,$09A8,$0A28,$0AA8,$0B28,$0BA8
word                = $0850,$08D0,$0950,$09D0,$0A50,$0AD0,$0B50,$0BD0
export word[] dgrbuff      = @dgr1rows, @dgr2rows
//
// Color mapping.
//
byte[] evnclr = $00,$88,$11,$99,$22,$AA,$33,$BB
byte[]        = $44,$CC,$55,$DD,$66,$EE,$77,$FF
byte[] oddclr = $00,$11,$22,$33,$44,$55,$66,$77
byte[]        = $88,$99,$AA,$BB,$CC,$DD,$EE,$FF

asm dgrInc(buff)
!SOURCE	"vmsrc/plvmzp.inc"
GBASL	=	$26
GBASH	=	$27
GBASE	=	GBASL
GCLR    =       $30
end
//
// Plot pixel
//
export asm dgrPlot(buff, x, y)
	; GET BUFFER ADDRESS
	STX	ESP
	LDA	ESTKL+2,X
	STA	SRCL
	LDA	ESTKH+2,X
	STA	SRCH
	LDA	ESTKL,X		; Y COORD
	AND	#$FE
	TAY
	LDA	(SRC),Y
	STA	GBASL
	INY
	LDA	(SRC),Y
	STA	GBASH
	LDA	ESTKL+1,X	; X COORD
	LSR	ESTKL,X
	LDX	GCLR		; COLOR
	PHP
	SEI
end
asm _dgrPlotPix
	JSR	$3000
	PLP
	LDX	ESP
	INX
	INX
	RTS
end
//
// Plot horizontal row of pixels
//
export asm dgrHLin(buff, x1, x2, y)
	; GET BUFFER ADDRESS
	STX	ESP
	LDA	ESTKL+3,X
	STA	SRCL
	LDA	ESTKH+3,X
	STA	SRCH
	LDA	ESTKL,X		; Y COORD
	AND	#$FE
	TAY
	LDA	(SRC),Y
	STA	GBASL
	INY
	LDA	(SRC),Y
	STA	GBASH
	LDA	ESTKL+2,X	; X1 COORD
	LSR	ESTKL,X
	PHP
-	PLP
	PHP
	SEI
	LDX	GCLR		; COLOR
end
asm _dgrHLinPix
	JSR	$3000
	LDX	ESP
	INC	ESTKL+2,X	; X1 COORD
	LDA	ESTKL+2,X
	CMP	ESTKL+1,X	; X2 COORD
	BCC	-
	BEQ	-
	PLP
	INX
	INX
	INX
	RTS
end
//
// Plot horizontal row of pixels
//
export asm dgrVLin(buff, x, y1, y2)
	; GET BUFFER ADDRESS
	STX	ESP
	LDA	ESTKL+3,X
	STA	SRCL
	LDA	ESTKH+3,X
	STA	SRCH
	LDA	ESTKL+1,X	; Y1 COORD
-	AND	#$FE
	TAY
	LDA	(SRC),Y
	STA	GBASL
	INY
	LDA	(SRC),Y
	STA	GBASH
	LDA	ESTKL+1,X
	LSR
	LDA	ESTKL+2,X	; X COORD
	LDX	GCLR		; COLOR
	PHP
	SEI
end
asm _dgrVLinPix
	JSR	$3000
	PLP
	LDX	ESP
	INC	ESTKL+1,X	; Y1 COORD
	LDA	ESTKL+1,X
	CMP	ESTKL,X		; Y2 COORD
	BCC	-
	BEQ	-
	INX
	INX
	INX
	RTS
end
//
// Draw sprite
//
export asm dgrBLT(buff, x, y, width, height, src)
	LDA	ESTKL,X		; SPRITE
	STA	SRCL
	LDA	ESTKH,X
	STA	SRCH
	LDA	ESTKL+5,X
	STA	DSTL
	LDA	ESTKH+5,X
	STA	DSTH
	LDA	ESTKL+4,X	; X1 COORD
	CMP	#80
	BPL	++++
	CLC
	ADC	ESTKL+2,X
	BMI	++++
	STA	ESTKH+2,X	; X2 COORD
	LDA	ESTKL+3,X	; Y1 COORD
	CMP	#48
	BPL	++++
	STA	ESTKH+3,X	; Y COORD
	CLC
	ADC	ESTKL+1,X
	BMI	++++
	STA	ESTKH+1,X	; Y2 COORD
	STX	ESP
	LDA	ESTKH+3,X
-	CMP	#48
	BCC	+
	LDA	SRCL		; SKIP TO NEXT ROW
	CLC
	ADC	ESTKL+2,X	; WIDTH
	STA	SRCL
	BCC	+++
	INC	SRCH
	BNE	+++
+	AND	#$FE
	TAY
	LDA	(DST),Y
	STA	GBASL
	INY
	LDA	(DST),Y
	STA	GBASH
	LDA	ESTKL+4,X	; X1 COORD
	STA	ESTKH+4,X	; X COORD
	PHP
	SEI
--	CMP	#80
	BCS	++
	STA	TMP
	LDA	ESTKH+3,X	; Y COORD
	LSR
	LDY	#$00
	LDA	(SRC),Y
        BMI	++
	TAX
	LDA	TMP
end
asm _dgrBLTPix
	JSR	$4000
	LDX	ESP
++	INC	SRCL
	BNE	+
	INC	SRCH
+	INC	ESTKH+4,X	; X COORD
	LDA	ESTKH+4,X
	BMI	--
	CMP	ESTKH+2,X	; X2 COORD
	BCC	--
	PLP
+++	INC	ESTKH+3,X	; Y COORD
	LDA	ESTKH+3,X
	BMI	-
	CMP	ESTKH+1,X	; Y2 COORD
	BCC	-
++++	INX
	INX
	INX
	INX
	INX
	RTS
end
//
// Internal set pixel routine
//
asm _dgrSetPix
	BCS	++
	; EVEN ROW
	LSR
	TAY
	BCS	+
end
asm _dgrSetEvnEvn
	; EVEN PIXEL
	JSR	$0100		; LDA AUX (DST),Y
	EOR	$2000,X
	AND	#$F0
	EOR	$2000,X
	STA	$C005		; WRITE AUX MEM
	STA	(GBASE),Y
	STA	$C004		; WRITE MAIN MEM
	RTS
end
asm _dgrSetEvnOdd
	; ODD PIXEL
+	LDA	(GBASE),Y
	EOR	$1000,X
	AND	#$F0
	EOR	$1000,X
	STA	(GBASE),Y
	RTS
	; ODD ROW
++	LSR
	TAY
	BCS	+++
end
asm _dgrSetOddEvn
	; EVEN PIXEL
	JSR	$0100		; LDA AUX (DST),Y
	EOR	$2000,X
	AND	#$0F
	EOR	$2000,X
	STA	$C005		; WRITE AUX MEM
	STA	(GBASE),Y
	STA	$C004		; WRITE MAIN MEM
	RTS
end
asm _dgrSetOddOdd
	; ODD PIXEL
+++	LDA	(GBASE),Y
	EOR	$1000,X
	AND	#$0F
	EOR	$1000,X
	STA	(GBASE),Y
	RTS
end
asm auxRead
	STA	$C003		; READ AUX MEM
	LDA	(GBASE),Y
	STA	$C002		; READ MAIN MEM
	RTS
end
//
// Draw 8x8 tile (forced to 2x2 block address)
//
export asm dgrTile(buff, x, y, src)
	STX	ESP
	LDA	ESTKL,X		; TILE
	STA	SRCL
	LDA	ESTKH,X
	STA	SRCH
	LDA	ESTKL+3,X
	STA	DSTL
	LDA	ESTKH+3,X
	STA	DSTH
	LDA	ESTKL+2,X	; X1 COORD
	CMP	#80
	BPL	++++
	CLC
	ADC	#$08
	BMI	++++
	STA	ESTKH+2,X	; X2 COORD
	LDA	ESTKL+1,X	; Y1 COORD
	CMP	#48
	BPL	++++
	STA	TMPL		; Y COORD
	CLC
	ADC	#$08
	BMI	++++
	STA	ESTKH+1,X	; Y2 COORD
	LDA	TMPL		; Y COORD
-	CMP	#48
	BCC	+
	LDA	SRCL		; SKIP TO NEXT ROW
	ADC	#$07		; CARRY = 1
	STA	SRCL
	BCC	+++
	INC	SRCH
	BNE	+++
+	AND	#$FE
	TAY
	LDA	(DST),Y
	STA	GBASL
	INY
	LDA	(DST),Y
	STA	GBASH
	LDA	ESTKL+2,X	; X1 COORD
	STA	TMPH		; X COORD
	PHP
	SEI
--	LSR
	TAY
	CMP	#40
	LDX	#$00
	LDA	(SRC,X)
	INC	SRCL
	BNE	+
	INC	SRCH
+	BCS	+
	STA	$C005		; WRITE AUX MEM
	STA	(GBASE),Y
	STA	$C004		; WRITE MAIN MEM
+	LDA	(SRC,X)
	INC	SRCL
	BNE	+
	INC	SRCH
+	BCS	++
	STA	(GBASE),Y
++	INC	TMPH		; X COORD
	INC	TMPH		; X COORD
	LDX	ESP
	LDA	TMPH
	BMI	--
	CMP	ESTKH+2,X	; X2 COORD
	BCC	--
	PLP
+++	INC	TMPL		; Y COORD
	INC	TMPL		; Y COORD
	LDA	TMPL
	BMI	-
	CMP	ESTKH+1,X	; Y2 COORD
	BCC	-
++++	INX
	INX
	INX
	RTS
end
//
// Draw a string of tiles
//
export asm dgrTileStr(buff, x, y, tilestr, strlen, tilebuff)
	DEX
-	DEX
	DEX
	DEX
	LDA	ESTKL+9,X	; BUFF
	STA	ESTKL+3,X
	LDA	ESTKH+9,X
	STA	ESTKH+3,X
	LDA	ESTKL+8,X	; X COORD
	STA	ESTKL+2,X
	LDA	ESTKL+7,X	; Y COORD
	STA	ESTKL+1,X
	LDA	ESTKL+4,X	; TILE
	STA	ESTKL,X
	LDA	ESTKH+4,X	; TILE
	STA	ESTKH,X
end
asm _dgrTileTile
	JSR	$5000
	LDA	ESTKL+5,X	; UPDATE X COORD
	CLC
	ADC	#$08
	CMP	#80		; OFF RIGHT SIDE
	BPL	+
	STA	ESTKL+5,X
	DEC	ESTKL+2,X	; DEC STRLEN
	BNE	-
+	INX
	INX
	INX
	INX
	INX
	INX
	RTS
end
//
// Draw a string of tiles
//
export asm dgrFill(buff, x, y, tile)
	LDA	ESTKL+2,X
	AND	#$0F
	STA	ESTKL+2,X
	LDA	ESTKL+1,X
	AND	#$0F
	STA	ESTKL+1,X
	LDA	#$00
	SEC
	SBC	ESTKL+2,X	; ORIGINAL X
	STA	ESTKL+2,X
	STA	ESTKH+2,X
	LDA	#$00
	SEC
	SBC	ESTKL+1,X
	STA	ESTKL+1,X
	DEX
-	DEX
	DEX
	DEX
	LDA	ESTKL+7,X	; BUFF
	STA	ESTKL+3,X
	LDA	ESTKH+7,X
	STA	ESTKH+3,X
	LDA	ESTKL+6,X	; X COORD
	STA	ESTKL+2,X
	LDA	ESTKL+5,X	; Y COORD
	STA	ESTKL+1,X
	LDA	ESTKL+4,X	; TILE
	STA	ESTKL,X
	LDA	ESTKH+4,X	; TILE
	STA	ESTKH,X
end
asm _dgrFillTile
	JSR	$5000
	LDA	ESTKL+3,X	; UPDATE X COORD
	CLC
	ADC	#$08
	STA	ESTKL+3,X
	CMP	#80		; OFF RIGHT SIDE?
	BMI	-
	LDA	ESTKH+3,X	; RESTORE X COORD
	STA	ESTKL+3,X
	LDA	ESTKL+2,X	; UPDATE Y COORD
	CLC
	ADC	#$08
	STA	ESTKL+2,X
	CMP	#48		; OFF BOTTOM?
	BMI	-
	INX
	INX
	INX
	INX
	RTS
end
//
// Wait for VLB
//
asm vlbWait
	DEX
-	LDA	$C019
	BMI	-
-	LDA	$C019
	BPL	-
	RTS
end
//
// Set double lores graphics, return draw buffer
//
export def dgrMode
	^showlores
	^showfull
	^showgraphics
	^showpage1
	^ena80  = 0
	^show80 = 0
//	^mapaux = 0
	^an3on
	return 1
end
//
// Set text mode
//
export def txtMode
	^showtext
	^showpage1
	^ena80   = 0
	^show40  = 0
	^mapmain = 0
	^an3on
	return call($FC58, 0, 0, 0, 0) // home()
end
//
// Set display page, return other page
//
export def dgrShow(page)
	page = page & 1
	^(showpage1 + page)
	return page ^ 1
end
//
// Set color for cear & plot routines
//
export def dgrColor(clr)
	^$30 = clr & $0F
end
//
// Draw line
//
export def dgrLine(buff, x1, y1, x2, y2)
	byte dx, dy, dx2, dy2, pp
	word sx, sy, err, dd2

	if x1 < x2
		sx = 1
		dx = x2 - x1
	else
		sx = -1
		dx = x1 - x2
	fin
	if y1 < y2
		sy = 1
		dy = y2 - y1
	else
		sy = -1
		dy = y1 - y2
	fin
	dx2 = dx << 1
	dy2 = dy << 1
	if dx >= dy
		// Horizontal line
		if sx < 0
			pp = x1
			x1 = x2
			x2 = pp
			pp = y1
			y1 = y2
			y2 = pp
			sy = -sy
		fin
		dd2 = dx2 - dy2
		err = dx - dy2
		sx = 1
		while dy
			if err < 0
				dgrHLin(buff, x1, x1 + sx - 1, y1)
				x1 = x1 + sx
				y1 = y1 + sy
				sx = 1
				dy--
				err = err + dd2
		        else
				sx++
				err = err - dy2
			fin
		loop
		if y2 == y1
			dgrHLin(buff, x1, x2, y1)
		fin
	else
		// Vertical line
		if sy < 0
			pp = x1
			x1 = x2
			x2 = pp
			pp = y1
			y1 = y2
			y2 = pp
			sx = -sx
		fin
		dd2 = dy2 - dx2
		err = dy - dx2
		sy = 1
		while dx
			if err < 0
				dgrVLin(buff, x1, y1, y1 + sy - 1)
				x1 = x1 + sx
				y1 = y1 + sy
				sy = 1
				dx--
				err = err + dd2
		        else
				sy++
				err = err - dx2
			fin
		loop
		if x2 == x1
			dgrVLin(buff, x1, y1, y2)
		fin
	fin
end
//
// Clear the buffer
//
export def dgrClear(buff, clr)
	byte[32] clrtile

	clr = evnclr[clr&$0F] | (oddclr[clr&$0F] << 8)
	memset(@clrtile, clr, 32)
	return dgrFill(buff, 0, 0, @clrtile)
end
//
// Make sure we are a 128K //e or //c
//
if MACHID & $F0 <> $B0
	puts("\n128K REQUIRED FOR DOUBLE LO-RES.")
	^$C010
	while ^$C000 < 128; loop
	return -1
fin
//
// Assembly fixups
//
_dgrPlotPix:1   = @_dgrSetPix
_dgrHLinPix:1   = @_dgrSetPix
_dgrVLinPix:1   = @_dgrSetPix
_dgrBLTPix:1    = @_dgrSetPix
_dgrTileTile:1  = @dgrTile
_dgrFillTile:1  = @dgrTile
_dgrSetEvnEvn:1 = @evnclr
_dgrSetEvnOdd:1 = @oddclr
_dgrSetOddEvn:1 = @evnclr
_dgrSetOddOdd:1 = @oddclr
// Put read AUX mem routine in scary location
memcpy($0100, @auxRead, 9)
done
