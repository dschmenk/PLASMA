import STDLIB
    predef syscall, call, memset, getc, putc, puts, putln
    byte MACHID
end

const FALSE  = 0
const TRUE   = !FALSE
const size   = 8190
const sizepl = size+1

byte flag[sizepl]
byte iter
word prime, i, k, count
byte strPrimes[] = " primes.\n"

def beep
  return putc(7)
end

def puti(i)
  if i < 0; putc('-'); i = -i; fin
  if i < 10
    putc(i + '0')
  else
    puti(i / 10)
    putc(i % 10 + '0')
  fin
end

beep
for iter = 1 to 10
  memset(@flag, sizepl, TRUE)
  count = 0
  for i = 0 to size
    if flag[i]
      prime = i + i + 3
      k = i + prime
      while k <= size
        flag[k] = FALSE
        k = k + prime
      loop
      count = count + 1
      ;puti(prime)
      ;putln
    fin
  next
next
beep
puti(count)
puts(@strPrimes)
done